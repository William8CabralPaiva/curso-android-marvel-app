plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
}

apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-parcelize'
apply from: "$rootDir/config/detekt/detekt.gradle"

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))


android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.example.marvelapp"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        //todo foi trocado o test runner para conseguir injetar a injeção de dependencia no test de implementação
        // antes era "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.example.marvelapp.CustomTestRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField("String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY'])
        buildConfigField("String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY'])
        buildConfigField("String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"')
    }

    testOptions {
        //gerencia melhot os testes
        //adicionar
//        androidTestImplementation 'androidx.test:runner:1.1.0'
//        androidTestUtil 'androidx.test:orchestrator:1.1.0'
        //todo cria teste teste de intrumentação pra cada teste fazendo com que eles funcione de forma isolada
        // e independente
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        staging {
            initWith debug //imita o debug
            applicationIdSuffix '.staging'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-staging.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(':core')// todo para conseguir ler o modulo core

    // AndroidX
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Material design
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Navigation
    def nav_version = '2.5.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewModel and LiveData
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Room
    def room_version = "2.4.3"// "2.4.3"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.room:room-paging:$room_version"//para funcionar paginação com o room

    // Paging3
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Glide
    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:31.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Other Libs
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"


    // Unit tests
    testImplementation project(":testing")
    testImplementation "androidx.room:room-testing:$room_version"

    // Instrumentation tests

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    def espresso_version = "3.3.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"


    // For instrumented tests.
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.9.3"//4.9.3

    def fragment_version = "1.5.5"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
}